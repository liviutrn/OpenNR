#include "Common/SharedData.hlsli"
#include "Common/VR.hlsli"

Texture2D<float4> InputTexture : register(t0);
Texture2D<float> DepthTexture : register(t1);

RWTexture2D<float4> OutputTexture : register(u0);
RWTexture2D<float4> MotionTexture : register(u1);

cbuffer BorderCB : register(b1)
{
	float4 BorderColor;  // color in xyz, depth threshold in w
	float4 Scale;        // xyzw: up, down, left, right
};

[numthreads(8, 8, 1)] void main(uint3 DTid : SV_DispatchThreadID) {
	float depth = DepthTexture[DTid.xy];
	float3 borderColor = BorderColor.xyz;
	float depthThreshold = BorderColor.w;
	if (depth > depthThreshold || depthThreshold == 0.0f) {
		float2 uv = (DTid.xy + 0.5f) * SharedData::BufferDim.zw;
		// Left/right border thresholds are eye-relative; in VR the packed stereo
		// buffer's raw x spans both eyes, so remap to the eye-local [0,1] range.
		uint eyeIndex = Stereo::GetEyeIndexFromTexCoord(uv);
		float2 eyeUV = Stereo::ConvertFromStereoUV(uv, eyeIndex);
		if (uv.y < Scale.x || uv.y > (1 - Scale.y) || eyeUV.x < Scale.z || eyeUV.x > (1 - Scale.w)) {
			OutputTexture[DTid.xy] = float4(borderColor, 1.0);
			MotionTexture[DTid.xy] = float4(0.0, 0.0, 0.0, 1.0);
		} else {
			OutputTexture[DTid.xy] = InputTexture[DTid.xy];
		}
	} else {
		OutputTexture[DTid.xy] = InputTexture[DTid.xy];
	}
}